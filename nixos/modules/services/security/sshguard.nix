{
  config,
  lib,
  pkgs,
  ...
}:
let
  cfg = config.services.sshguard;

  configFile =
    let
      args = lib.concatStringsSep " " (
        [
          "-afb"
          "-p info"
          "-o cat"
          "-n1"
        ]
        ++ (map (name: "-t ${lib.escapeShellArg name}") cfg.services)
      );
      backend = if config.networking.nftables.enable then "sshg-fw-nft-sets" else "sshg-fw-ipset";
    in
    pkgs.writeText "sshguard.conf" ''
      BACKEND="${pkgs.sshguard}/libexec/${backend}"
      LOGREADER="LANG=C ${config.systemd.package}/bin/journalctl ${args}"
    '';

in
{

  ###### interface

  options = {

    services.sshguard = {
      enable = lib.mkOption {
        default = false;
        description = "Whether to enable the sshguard service.";
        type = lib.types.bool;
      };

      attack_threshold = lib.mkOption {
        default = 30;

        description = ''
          Block attackers when their cumulative attack score exceeds threshold. Most attacks have a score of 10.
        '';

        type = lib.types.int;
      };

      blacklist_file = lib.mkOption {
        default = "/var/lib/sshguard/blacklist.db";

        description = ''
          Blacklist an attacker when its score exceeds threshold. Blacklisted addresses are loaded from and added to blacklist-file.
        '';

        type = lib.types.path;
      };

      blacklist_threshold = lib.mkOption {
        default = null;

        description = ''
          Blacklist an attacker when its score exceeds threshold. Blacklisted addresses are loaded from and added to blacklist-file.
        '';

        example = 120;
        type = lib.types.nullOr lib.types.int;
      };

      blocktime = lib.mkOption {
        default = 120;

        description = ''
          Block attackers for initially blocktime seconds after exceeding threshold. Subsequent blocks increase by a factor of 1.5.

          sshguard unblocks attacks at random intervals, so actual block times will be longer.
        '';

        type = lib.types.int;
      };

      detection_time = lib.mkOption {
        default = 1800;

        description = ''
          Remember potential attackers for up to detection_time seconds before resetting their score.
        '';

        type = lib.types.int;
      };

      services = lib.mkOption {
        default = [ "sshd" ];

        description = ''
          Systemd services sshguard should receive logs of.
        '';

        example = [
          "sshd"
          "exim"
        ];

        type = lib.types.listOf lib.types.str;
      };

      whitelist = lib.mkOption {
        default = [ ];

        description = ''
          Whitelist a list of addresses, hostnames, or address blocks.
        '';

        example = [
          "198.51.100.56"
          "198.51.100.2"
        ];

        type = lib.types.listOf lib.types.str;
      };
    };
  };

  ###### implementation

  config = lib.mkIf cfg.enable {

    environment.etc."sshguard.conf".source = configFile;

    systemd.services.sshguard = {
      after = [ "network.target" ];
      description = "SSHGuard brute-force attacks protection system";
      partOf = lib.optional config.networking.firewall.enable "firewall.service";

      path =
        with pkgs;
        if config.networking.nftables.enable then
          [
            nftables
            iproute2
            systemd
          ]
        else
          [
            iptables
            ipset
            iproute2
            systemd
          ];

      postStop =
        lib.optionalString config.networking.firewall.enable ''
          ${pkgs.iptables}/bin/iptables  -D INPUT -m set --match-set sshguard4 src -j DROP
          ${pkgs.ipset}/bin/ipset -quiet destroy sshguard4
        ''
        + lib.optionalString (config.networking.firewall.enable && config.networking.enableIPv6) ''
          ${pkgs.iptables}/bin/ip6tables -D INPUT -m set --match-set sshguard6 src -j DROP
          ${pkgs.ipset}/bin/ipset -quiet destroy sshguard6
        '';

      # The sshguard ipsets must exist before we invoke
      # iptables. sshguard creates the ipsets after startup if
      # necessary, but if we let sshguard do it, we can't reliably add
      # the iptables rules because postStart races with the creation
      # of the ipsets. So instead, we create both the ipsets and
      # firewall rules before sshguard starts.
      preStart =
        lib.optionalString config.networking.firewall.enable ''
          ${pkgs.ipset}/bin/ipset -quiet create -exist sshguard4 hash:net family inet
          ${pkgs.iptables}/bin/iptables  -I INPUT -m set --match-set sshguard4 src -j DROP
        ''
        + lib.optionalString (config.networking.firewall.enable && config.networking.enableIPv6) ''
          ${pkgs.ipset}/bin/ipset -quiet create -exist sshguard6 hash:net family inet6
          ${pkgs.iptables}/bin/ip6tables -I INPUT -m set --match-set sshguard6 src -j DROP
        '';

      restartTriggers = [ configFile ];

      serviceConfig = {
        CapabilityBoundingSet = "CAP_NET_ADMIN CAP_NET_RAW";

        ExecStart =
          let
            args = lib.concatStringsSep " " (
              [
                "-a ${toString cfg.attack_threshold}"
                "-p ${toString cfg.blocktime}"
                "-s ${toString cfg.detection_time}"
                (lib.optionalString (
                  cfg.blacklist_threshold != null
                ) "-b ${toString cfg.blacklist_threshold}:${cfg.blacklist_file}")
              ]
              ++ (map (name: "-w ${lib.escapeShellArg name}") cfg.whitelist)
            );
          in
          "${pkgs.sshguard}/bin/sshguard ${args}";

        ProtectHome = "tmpfs";
        ProtectSystem = "strict";
        Restart = "always";
        RuntimeDirectory = "sshguard";
        StateDirectory = "sshguard";
        Type = "simple";
      };

      unitConfig.Documentation = "man:sshguard(8)";
      wantedBy = [ "multi-user.target" ];
    };
  };
}
