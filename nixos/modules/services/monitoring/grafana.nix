{
  config,
  lib,
  pkgs,
  options,
  ...
}:

with lib;

let
  cfg = config.services.grafana;
  opt = options.services.grafana;
  provisioningSettingsFormat = pkgs.formats.yaml { };
  declarativePlugins = pkgs.linkFarm "grafana-plugins" (
    map (pkg: {
      name = pkg.pname;
      path = pkg;
    }) cfg.declarativePlugins
  );
  useMysql = cfg.settings.database.type == "mysql";
  usePostgresql = cfg.settings.database.type == "postgres";

  # Prefer using the values from the default config file[0] directly. This way,
  # people reading the NixOS manual can see them without cross-referencing the
  # official documentation.
  #
  # However, if there is no default entry or if the setting is optional, use
  # `null` as the default value. It will be turned into the empty string.
  #
  # If a setting is a list, always allow setting it as a plain string as well.
  #
  # [0]: https://github.com/grafana/grafana/blob/main/conf/defaults.ini
  settingsFormatIni = pkgs.formats.ini {
    listToValue = concatMapStringsSep " " (generators.mkValueStringDefault { });

    mkKeyValue = generators.mkKeyValueDefault {
      mkValueString = v: if v == null then "" else generators.mkValueStringDefault { } v;
    } "=";
  };
  configFile = settingsFormatIni.generate "config.ini" cfg.settings;

  mkProvisionCfg =
    name: attr: provisionCfg:
    if provisionCfg.path != null then
      provisionCfg.path
    else
      provisioningSettingsFormat.generate "${name}.yaml" (
        if provisionCfg.settings != null then
          provisionCfg.settings
        else
          {
            ${attr} = [ ];
            apiVersion = 1;
          }
      );

  datasourceFileOrDir = mkProvisionCfg "datasource" "datasources" cfg.provision.datasources;
  dashboardFileOrDir = mkProvisionCfg "dashboard" "providers" cfg.provision.dashboards;

  generateAlertingProvisioningYaml =
    x:
    if (cfg.provision.alerting."${x}".path == null) then
      provisioningSettingsFormat.generate "${x}.yaml" cfg.provision.alerting."${x}".settings
    else
      cfg.provision.alerting."${x}".path;
  rulesFileOrDir = generateAlertingProvisioningYaml "rules";
  contactPointsFileOrDir = generateAlertingProvisioningYaml "contactPoints";
  policiesFileOrDir = generateAlertingProvisioningYaml "policies";
  templatesFileOrDir = generateAlertingProvisioningYaml "templates";
  muteTimingsFileOrDir = generateAlertingProvisioningYaml "muteTimings";

  ln =
    {
      dir,
      filename,
      src,
    }:
    ''
      if [[ -d "${src}" ]]; then
        pushd $out/${dir} &>/dev/null
          lndir "${src}"
        popd &>/dev/null
      else
        ln -sf ${src} $out/${dir}/${filename}.yaml
      fi
    '';
  provisionConfDir =
    pkgs.runCommand "grafana-provisioning"
      {
        nativeBuildInputs = [ pkgs.lndir ];
      }
      ''
        mkdir -p $out/{alerting,datasources,dashboards,plugins}
        ${ln {
          dir = "datasources";
          filename = "datasource";
          src = datasourceFileOrDir;
        }}
        ${ln {
          dir = "dashboards";
          filename = "dashboard";
          src = dashboardFileOrDir;
        }}
        ${ln {
          dir = "alerting";
          filename = "rules";
          src = rulesFileOrDir;
        }}
        ${ln {
          dir = "alerting";
          filename = "contactPoints";
          src = contactPointsFileOrDir;
        }}
        ${ln {
          dir = "alerting";
          filename = "policies";
          src = policiesFileOrDir;
        }}
        ${ln {
          dir = "alerting";
          filename = "templates";
          src = templatesFileOrDir;
        }}
        ${ln {
          dir = "alerting";
          filename = "muteTimings";
          src = muteTimingsFileOrDir;
        }}
      '';

  # Get a submodule without any embedded metadata:
  _filter = x: removeAttrs x [ "_module" ];

  # https://grafana.com/docs/grafana/latest/administration/provisioning/#datasources
  grafanaTypes.datasourceConfig = types.submodule {
    options = {
      access = mkOption {
        default = "proxy";
        description = "Access mode. proxy or direct (Server or Browser in the UI). Required.";

        type = types.enum [
          "proxy"
          "direct"
        ];
      };

      editable = mkOption {
        default = false;
        description = "Allow users to edit datasources from the UI.";
        type = types.bool;
      };

      jsonData = mkOption {
        default = null;
        description = "Extra data for datasource plugins.";
        type = types.nullOr types.attrs;
      };

      name = mkOption {
        description = "Name of the datasource. Required.";
        type = types.str;
      };

      secureJsonData = mkOption {
        default = null;

        description = ''
          Datasource specific secure configuration. Please note that the contents of this option
          will end up in a world-readable Nix store. Use the file provider
          pointing at a reasonably secured file in the local filesystem
          to work around that. Look at the documentation for details:
          <https://grafana.com/docs/grafana/latest/setup-grafana/configure-grafana/#file-provider>
        '';

        type = types.nullOr types.attrs;
      };

      type = mkOption {
        description = "Datasource type. Required.";
        type = types.str;
      };

      uid = mkOption {
        default = null;
        description = "Custom UID which can be used to reference this datasource in other parts of the configuration, if not specified will be generated automatically.";
        type = types.nullOr types.str;
      };

      url = mkOption {
        default = "";
        description = "Url of the datasource.";
        type = types.str;
      };
    };

    freeformType = provisioningSettingsFormat.type;
  };

  # https://grafana.com/docs/grafana/latest/administration/provisioning/#dashboards
  grafanaTypes.dashboardConfig = types.submodule {
    options = {
      options.path = mkOption {
        description = "Path grafana will watch for dashboards. Required when using the 'file' type.";
        type = types.path;
      };

      name = mkOption {
        default = "default";
        description = "A unique provider name.";
        type = types.str;
      };

      type = mkOption {
        default = "file";
        description = "Dashboard provider type.";
        type = types.str;
      };
    };

    freeformType = provisioningSettingsFormat.type;
  };
in
{
  imports = [
    (mkRemovedOptionModule [ "services" "grafana" "provision" "notifiers" ] ''
      Notifiers (services.grafana.provision.notifiers) were removed in Grafana 11.
    '')

    (mkRenamedOptionModule
      [ "services" "grafana" "protocol" ]
      [ "services" "grafana" "settings" "server" "protocol" ]
    )
    (mkRenamedOptionModule
      [ "services" "grafana" "addr" ]
      [ "services" "grafana" "settings" "server" "http_addr" ]
    )
    (mkRenamedOptionModule
      [ "services" "grafana" "port" ]
      [ "services" "grafana" "settings" "server" "http_port" ]
    )
    (mkRenamedOptionModule
      [ "services" "grafana" "domain" ]
      [ "services" "grafana" "settings" "server" "domain" ]
    )
    (mkRenamedOptionModule
      [ "services" "grafana" "rootUrl" ]
      [ "services" "grafana" "settings" "server" "root_url" ]
    )
    (mkRenamedOptionModule
      [ "services" "grafana" "staticRootPath" ]
      [ "services" "grafana" "settings" "server" "static_root_path" ]
    )
    (mkRenamedOptionModule
      [ "services" "grafana" "certFile" ]
      [ "services" "grafana" "settings" "server" "cert_file" ]
    )
    (mkRenamedOptionModule
      [ "services" "grafana" "certKey" ]
      [ "services" "grafana" "settings" "server" "cert_key" ]
    )
    (mkRenamedOptionModule
      [ "services" "grafana" "socket" ]
      [ "services" "grafana" "settings" "server" "socket" ]
    )
    (mkRenamedOptionModule
      [ "services" "grafana" "database" "type" ]
      [ "services" "grafana" "settings" "database" "type" ]
    )
    (mkRenamedOptionModule
      [ "services" "grafana" "database" "host" ]
      [ "services" "grafana" "settings" "database" "host" ]
    )
    (mkRenamedOptionModule
      [ "services" "grafana" "database" "name" ]
      [ "services" "grafana" "settings" "database" "name" ]
    )
    (mkRenamedOptionModule
      [ "services" "grafana" "database" "user" ]
      [ "services" "grafana" "settings" "database" "user" ]
    )
    (mkRenamedOptionModule
      [ "services" "grafana" "database" "password" ]
      [ "services" "grafana" "settings" "database" "password" ]
    )
    (mkRenamedOptionModule
      [ "services" "grafana" "database" "path" ]
      [ "services" "grafana" "settings" "database" "path" ]
    )
    (mkRenamedOptionModule
      [ "services" "grafana" "database" "connMaxLifetime" ]
      [ "services" "grafana" "settings" "database" "conn_max_lifetime" ]
    )
    (mkRenamedOptionModule
      [ "services" "grafana" "security" "adminUser" ]
      [ "services" "grafana" "settings" "security" "admin_user" ]
    )
    (mkRenamedOptionModule
      [ "services" "grafana" "security" "adminPassword" ]
      [ "services" "grafana" "settings" "security" "admin_password" ]
    )
    (mkRenamedOptionModule
      [ "services" "grafana" "security" "secretKey" ]
      [ "services" "grafana" "settings" "security" "secret_key" ]
    )
    (mkRenamedOptionModule
      [ "services" "grafana" "server" "serveFromSubPath" ]
      [ "services" "grafana" "settings" "server" "serve_from_sub_path" ]
    )
    (mkRenamedOptionModule
      [ "services" "grafana" "smtp" "enable" ]
      [ "services" "grafana" "settings" "smtp" "enabled" ]
    )
    (mkRenamedOptionModule
      [ "services" "grafana" "smtp" "user" ]
      [ "services" "grafana" "settings" "smtp" "user" ]
    )
    (mkRenamedOptionModule
      [ "services" "grafana" "smtp" "password" ]
      [ "services" "grafana" "settings" "smtp" "password" ]
    )
    (mkRenamedOptionModule
      [ "services" "grafana" "smtp" "fromAddress" ]
      [ "services" "grafana" "settings" "smtp" "from_address" ]
    )
    (mkRenamedOptionModule
      [ "services" "grafana" "users" "allowSignUp" ]
      [ "services" "grafana" "settings" "users" "allow_sign_up" ]
    )
    (mkRenamedOptionModule
      [ "services" "grafana" "users" "allowOrgCreate" ]
      [ "services" "grafana" "settings" "users" "allow_org_create" ]
    )
    (mkRenamedOptionModule
      [ "services" "grafana" "users" "autoAssignOrg" ]
      [ "services" "grafana" "settings" "users" "auto_assign_org" ]
    )
    (mkRenamedOptionModule
      [ "services" "grafana" "users" "autoAssignOrgRole" ]
      [ "services" "grafana" "settings" "users" "auto_assign_org_role" ]
    )
    (mkRenamedOptionModule
      [ "services" "grafana" "auth" "disableLoginForm" ]
      [ "services" "grafana" "settings" "auth" "disable_login_form" ]
    )
    (mkRenamedOptionModule
      [ "services" "grafana" "auth" "anonymous" "enable" ]
      [ "services" "grafana" "settings" "auth.anonymous" "enabled" ]
    )
    (mkRenamedOptionModule
      [ "services" "grafana" "auth" "anonymous" "org_name" ]
      [ "services" "grafana" "settings" "auth.anonymous" "org_name" ]
    )
    (mkRenamedOptionModule
      [ "services" "grafana" "auth" "anonymous" "org_role" ]
      [ "services" "grafana" "settings" "auth.anonymous" "org_role" ]
    )
    (mkRenamedOptionModule
      [ "services" "grafana" "auth" "azuread" "enable" ]
      [ "services" "grafana" "settings" "auth.azuread" "enabled" ]
    )
    (mkRenamedOptionModule
      [ "services" "grafana" "auth" "azuread" "allowSignUp" ]
      [ "services" "grafana" "settings" "auth.azuread" "allow_sign_up" ]
    )
    (mkRenamedOptionModule
      [ "services" "grafana" "auth" "azuread" "clientId" ]
      [ "services" "grafana" "settings" "auth.azuread" "client_id" ]
    )
    (mkRenamedOptionModule
      [ "services" "grafana" "auth" "azuread" "allowedDomains" ]
      [ "services" "grafana" "settings" "auth.azuread" "allowed_domains" ]
    )
    (mkRenamedOptionModule
      [ "services" "grafana" "auth" "azuread" "allowedGroups" ]
      [ "services" "grafana" "settings" "auth.azuread" "allowed_groups" ]
    )
    (mkRenamedOptionModule
      [ "services" "grafana" "auth" "google" "enable" ]
      [ "services" "grafana" "settings" "auth.google" "enabled" ]
    )
    (mkRenamedOptionModule
      [ "services" "grafana" "auth" "google" "allowSignUp" ]
      [ "services" "grafana" "settings" "auth.google" "allow_sign_up" ]
    )
    (mkRenamedOptionModule
      [ "services" "grafana" "auth" "google" "clientId" ]
      [ "services" "grafana" "settings" "auth.google" "client_id" ]
    )
    (mkRenamedOptionModule
      [ "services" "grafana" "analytics" "reporting" "enable" ]
      [ "services" "grafana" "settings" "analytics" "reporting_enabled" ]
    )

    (mkRemovedOptionModule [ "services" "grafana" "database" "passwordFile" ] ''
      This option has been removed. Use 'services.grafana.settings.database.password' with file provider instead.
    '')
    (mkRemovedOptionModule [ "services" "grafana" "security" "adminPasswordFile" ] ''
      This option has been removed. Use 'services.grafana.settings.security.admin_password' with file provider instead.
    '')
    (mkRemovedOptionModule [ "services" "grafana" "security" "secretKeyFile" ] ''
      This option has been removed. Use 'services.grafana.settings.security.secret_key' with file provider instead.
    '')
    (mkRemovedOptionModule [ "services" "grafana" "smtp" "passwordFile" ] ''
      This option has been removed. Use 'services.grafana.settings.smtp.password' with file provider instead.
    '')
    (mkRemovedOptionModule [ "services" "grafana" "auth" "azuread" "clientSecretFile" ] ''
      This option has been removed. Use 'services.grafana.settings.azuread.client_secret' with file provider instead.
    '')
    (mkRemovedOptionModule [ "services" "grafana" "auth" "google" "clientSecretFile" ] ''
      This option has been removed. Use 'services.grafana.settings.google.client_secret' with file provider instead.
    '')
    (mkRemovedOptionModule [ "services" "grafana" "extraOptions" ] ''
      This option has been removed. Use 'services.grafana.settings' instead. For a detailed migration guide, please
      review the release notes of NixOS 22.11.
    '')

    (mkRemovedOptionModule [
      "services"
      "grafana"
      "auth"
      "azuread"
      "tenantId"
    ] "This option has been deprecated upstream.")
  ];

  options.services.grafana = {
    enable = mkEnableOption "grafana";
    package = mkPackageOption pkgs "grafana" { };

    dataDir = mkOption {
      default = "/var/lib/grafana";
      description = "Data directory.";
      type = types.path;
    };

    declarativePlugins = mkOption {
      # Make sure each plugin is added only once; otherwise building
      # the link farm fails, since the same path is added multiple
      # times.
      apply = x: if isList x then lib.unique x else x;
      default = null;

      description = ''
        If non-null, then a list of packages containing Grafana plugins to install. If set, plugins cannot
        be manually installed.

        Keep in mind that this turns off drilldown: for this to work, you need to add
        `grafana-metricsdrilldown-app`, `grafana-lokiexplore-app`, `grafana-exploretraces-app`
        and `grafana-pyroscope-app` to this option.
      '';

      example = literalExpression "with pkgs.grafanaPlugins; [ grafana-piechart-panel ]";
      type = with types; nullOr (listOf path);
    };

    openFirewall = mkOption {
      default = false;
      description = "Open the ports in the firewall for the server.";
      type = types.bool;
    };

    provision = {
      enable = mkEnableOption "provision";

      alerting = {
        contactPoints = {
          path = mkOption {
            default = null;

            description = ''
              Path to YAML contact points configuration. Can't be used with
              [](#opt-services.grafana.provision.alerting.contactPoints.settings) simultaneously.
              Can be either a directory or a single YAML file. Will end up in the store.
            '';

            type = types.nullOr types.path;
          };

          settings = mkOption {
            default = null;

            description = ''
              Grafana contact points configuration in Nix. Can't be used with
              [](#opt-services.grafana.provision.alerting.contactPoints.path) simultaneously. See
              <https://grafana.com/docs/grafana/latest/administration/provisioning/#contact-points>
              for supported options.
            '';

            example = literalExpression ''
              {
                apiVersion = 1;

                contactPoints = [{
                  orgId = 1;
                  name = "cp_1";
                  receivers = [{
                    uid = "first_uid";
                    type = "prometheus-alertmanager";
                    settings.url = "http://test:9000";
                  }];
                }];

                deleteContactPoints = [{
                  orgId = 1;
                  uid = "first_uid";
                }];
              }
            '';

            type = types.nullOr (
              types.submodule {
                options = {
                  apiVersion = mkOption {
                    default = 1;
                    description = "Config file version.";
                    type = types.int;
                  };

                  contactPoints = mkOption {
                    default = [ ];
                    description = "List of contact points to import or update.";

                    type = types.listOf (
                      types.submodule {
                        options.name = mkOption {
                          description = "Name of the contact point. Required.";
                          type = types.str;
                        };

                        freeformType = provisioningSettingsFormat.type;
                      }
                    );
                  };

                  deleteContactPoints = mkOption {
                    default = [ ];
                    description = "List of receivers that should be deleted.";

                    type = types.listOf (
                      types.submodule {
                        options.orgId = mkOption {
                          default = 1;
                          description = "Organization ID, default = 1.";
                          type = types.int;
                        };

                        options.uid = mkOption {
                          description = "Unique identifier for the receiver. Required.";
                          type = types.str;
                        };
                      }
                    );
                  };
                };
              }
            );
          };
        };

        muteTimings = {
          path = mkOption {
            default = null;

            description = ''
              Path to YAML mute timings configuration. Can't be used with
              [](#opt-services.grafana.provision.alerting.muteTimings.settings) simultaneously.
              Can be either a directory or a single YAML file. Will end up in the store.
            '';

            type = types.nullOr types.path;
          };

          settings = mkOption {
            default = null;

            description = ''
              Grafana mute timings configuration in Nix. Can't be used with
              [](#opt-services.grafana.provision.alerting.muteTimings.path) simultaneously. See
              <https://grafana.com/docs/grafana/latest/administration/provisioning/#mute-timings>
              for supported options.
            '';

            example = literalExpression ''
              {
                apiVersion = 1;

                muteTimes = [{
                  orgId = 1;
                  name = "mti_1";
                  time_intervals = [{
                    times = [{
                      start_time = "06:00";
                      end_time = "23:59";
                    }];
                    weekdays = [
                      "monday:wednesday"
                      "saturday"
                      "sunday"
                    ];
                    months = [
                      "1:3"
                      "may:august"
                      "december"
                    ];
                    years = [
                      "2020:2022"
                      "2030"
                    ];
                    days_of_month = [
                      "1:5"
                      "-3:-1"
                    ];
                  }];
                }];

                deleteMuteTimes = [{
                  orgId = 1;
                  name = "mti_1";
                }];
              }
            '';

            type = types.nullOr (
              types.submodule {
                options = {
                  apiVersion = mkOption {
                    default = 1;
                    description = "Config file version.";
                    type = types.int;
                  };

                  deleteMuteTimes = mkOption {
                    default = [ ];
                    description = "List of mute time intervals that should be deleted.";

                    type = types.listOf (
                      types.submodule {
                        options.name = mkOption {
                          description = "Name of the mute time interval, must be unique. Required.";
                          type = types.str;
                        };

                        options.orgId = mkOption {
                          default = 1;
                          description = "Organization ID, default = 1.";
                          type = types.int;
                        };
                      }
                    );
                  };

                  muteTimes = mkOption {
                    default = [ ];
                    description = "List of mute time intervals to import or update.";

                    type = types.listOf (
                      types.submodule {
                        options.name = mkOption {
                          description = "Name of the mute time interval, must be unique. Required.";
                          type = types.str;
                        };

                        freeformType = provisioningSettingsFormat.type;
                      }
                    );
                  };
                };
              }
            );
          };
        };

        policies = {
          path = mkOption {
            default = null;

            description = ''
              Path to YAML notification policies configuration. Can't be used with
              [](#opt-services.grafana.provision.alerting.policies.settings) simultaneously.
              Can be either a directory or a single YAML file. Will end up in the store.
            '';

            type = types.nullOr types.path;
          };

          settings = mkOption {
            default = null;

            description = ''
              Grafana notification policies configuration in Nix. Can't be used with
              [](#opt-services.grafana.provision.alerting.policies.path) simultaneously. See
              <https://grafana.com/docs/grafana/latest/administration/provisioning/#notification-policies>
              for supported options.
            '';

            example = literalExpression ''
              {
                apiVersion = 1;

                policies = [{
                  orgId = 1;
                  receiver = "grafana-default-email";
                  group_by = [ "..." ];
                  matchers = [
                    "alertname = Watchdog"
                    "severity =~ \"warning|critical\""
                  ];
                  mute_time_intervals = [
                    "abc"
                  ];
                  group_wait = "30s";
                  group_interval = "5m";
                  repeat_interval = "4h";
                }];

                resetPolicies = [
                  1
                ];
              }
            '';

            type = types.nullOr (
              types.submodule {
                options = {
                  apiVersion = mkOption {
                    default = 1;
                    description = "Config file version.";
                    type = types.int;
                  };

                  policies = mkOption {
                    default = [ ];
                    description = "List of contact points to import or update.";

                    type = types.listOf (
                      types.submodule {
                        freeformType = provisioningSettingsFormat.type;
                      }
                    );
                  };

                  resetPolicies = mkOption {
                    default = [ ];
                    description = "List of orgIds that should be reset to the default policy.";
                    type = types.listOf types.int;
                  };
                };
              }
            );
          };
        };

        rules = {
          path = mkOption {
            default = null;

            description = ''
              Path to YAML rules configuration. Can't be used with
              [](#opt-services.grafana.provision.alerting.rules.settings) simultaneously.
              Can be either a directory or a single YAML file. Will end up in the store.
            '';

            type = types.nullOr types.path;
          };

          settings = mkOption {
            default = null;

            description = ''
              Grafana rules configuration in Nix. Can't be used with
              [](#opt-services.grafana.provision.alerting.rules.path) simultaneously. See
              <https://grafana.com/docs/grafana/latest/administration/provisioning/#rules>
              for supported options.
            '';

            example = literalExpression ''
              {
                apiVersion = 1;

                groups = [{
                  orgId = 1;
                  name = "my_rule_group";
                  folder = "my_first_folder";
                  interval = "60s";
                  rules = [{
                    uid = "my_id_1";
                    title = "my_first_rule";
                    condition = "A";
                    data = [{
                      refId = "A";
                      datasourceUid = "-100";
                      model = {
                        conditions = [{
                          evaluator = {
                            params = [ 3 ];
                            type = "git";
                          };
                          operator.type = "and";
                          query.params = [ "A" ];
                          reducer.type = "last";
                          type = "query";
                        }];
                        datasource = {
                          type = "__expr__";
                          uid = "-100";
                        };
                        expression = "1==0";
                        intervalMs = 1000;
                        maxDataPoints = 43200;
                        refId = "A";
                        type = "math";
                      };
                    }];
                    dashboardUid = "my_dashboard";
                    panelId = 123;
                    noDataState = "Alerting";
                    for = "60s";
                    annotations.some_key = "some_value";
                    labels.team = "sre_team1";
                  }];
                }];

                deleteRules = [{
                  orgId = 1;
                  uid = "my_id_1";
                }];
              }
            '';

            type = types.nullOr (
              types.submodule {
                options = {
                  apiVersion = mkOption {
                    default = 1;
                    description = "Config file version.";
                    type = types.int;
                  };

                  deleteRules = mkOption {
                    default = [ ];
                    description = "List of alert rule UIDs that should be deleted.";

                    type = types.listOf (
                      types.submodule {
                        options.orgId = mkOption {
                          default = 1;
                          description = "Organization ID, default = 1";
                          type = types.int;
                        };

                        options.uid = mkOption {
                          description = "Unique identifier for the rule. Required.";
                          type = types.str;
                        };
                      }
                    );
                  };

                  groups = mkOption {
                    default = [ ];
                    description = "List of rule groups to import or update.";

                    type = types.listOf (
                      types.submodule {
                        options.folder = mkOption {
                          description = "Name of the folder the rule group will be stored in. Required.";
                          type = types.str;
                        };

                        options.interval = mkOption {
                          description = "Interval that the rule group should be evaluated at. Required.";
                          type = types.str;
                        };

                        options.name = mkOption {
                          description = "Name of the rule group. Required.";
                          type = types.str;
                        };

                        freeformType = provisioningSettingsFormat.type;
                      }
                    );
                  };
                };
              }
            );
          };
        };

        templates = {
          path = mkOption {
            default = null;

            description = ''
              Path to YAML templates configuration. Can't be used with
              [](#opt-services.grafana.provision.alerting.templates.settings) simultaneously.
              Can be either a directory or a single YAML file. Will end up in the store.
            '';

            type = types.nullOr types.path;
          };

          settings = mkOption {
            default = null;

            description = ''
              Grafana templates configuration in Nix. Can't be used with
              [](#opt-services.grafana.provision.alerting.templates.path) simultaneously. See
              <https://grafana.com/docs/grafana/latest/administration/provisioning/#templates>
              for supported options.
            '';

            example = literalExpression ''
              {
                apiVersion = 1;

                templates = [{
                  orgId = 1;
                  name = "my_first_template";
                  template = "Alerting with a custom text template";
                }];

                deleteTemplates = [{
                  orgId = 1;
                  name = "my_first_template";
                }];
              }
            '';

            type = types.nullOr (
              types.submodule {
                options = {
                  apiVersion = mkOption {
                    default = 1;
                    description = "Config file version.";
                    type = types.int;
                  };

                  deleteTemplates = mkOption {
                    default = [ ];
                    description = "List of alert rule UIDs that should be deleted.";

                    type = types.listOf (
                      types.submodule {
                        options.name = mkOption {
                          description = "Name of the template, must be unique. Required.";
                          type = types.str;
                        };

                        options.orgId = mkOption {
                          default = 1;
                          description = "Organization ID, default = 1.";
                          type = types.int;
                        };
                      }
                    );
                  };

                  templates = mkOption {
                    default = [ ];
                    description = "List of templates to import or update.";

                    type = types.listOf (
                      types.submodule {
                        options.name = mkOption {
                          description = "Name of the template, must be unique. Required.";
                          type = types.str;
                        };

                        options.template = mkOption {
                          description = "Alerting with a custom text template";
                          type = types.str;
                        };

                        freeformType = provisioningSettingsFormat.type;
                      }
                    );
                  };
                };
              }
            );
          };
        };
      };

      dashboards = mkOption {
        default = { };

        description = ''
          Declaratively provision Grafana's dashboards.
        '';

        type = types.submodule {
          options.path = mkOption {
            default = null;

            description = ''
              Path to YAML dashboard configuration. Can't be used with
              [](#opt-services.grafana.provision.dashboards.settings) simultaneously.
              Can be either a directory or a single YAML file. Will end up in the store.
            '';

            type = types.nullOr types.path;
          };

          options.settings = mkOption {
            default = null;

            description = ''
              Grafana dashboard configuration in Nix. Can't be used with
              [](#opt-services.grafana.provision.dashboards.path) simultaneously. See
              <https://grafana.com/docs/grafana/latest/administration/provisioning/#dashboards>
              for supported options.
            '';

            example = literalExpression ''
              {
                apiVersion = 1;

                providers = [{
                    name = "default";
                    options.path = "/var/lib/grafana/dashboards";
                }];
              }
            '';

            type = types.nullOr (
              types.submodule {
                options.apiVersion = mkOption {
                  default = 1;
                  description = "Config file version.";
                  type = types.int;
                };

                options.providers = mkOption {
                  default = [ ];
                  description = "List of dashboards to insert/update.";
                  type = types.listOf grafanaTypes.dashboardConfig;
                };
              }
            );
          };
        };
      };

      datasources = mkOption {
        default = { };

        description = ''
          Declaratively provision Grafana's datasources.
        '';

        type = types.submodule {
          options.path = mkOption {
            default = null;

            description = ''
              Path to YAML datasource configuration. Can't be used with
              [](#opt-services.grafana.provision.datasources.settings) simultaneously.
              Can be either a directory or a single YAML file. Will end up in the store.
            '';

            type = types.nullOr types.path;
          };

          options.settings = mkOption {
            default = null;

            description = ''
              Grafana datasource configuration in Nix. Can't be used with
              [](#opt-services.grafana.provision.datasources.path) simultaneously. See
              <https://grafana.com/docs/grafana/latest/administration/provisioning/#data-sources>
              for supported options.
            '';

            example = literalExpression ''
              {
                apiVersion = 1;

                datasources = [{
                  name = "Graphite";
                  type = "graphite";
                }];

                deleteDatasources = [{
                  name = "Graphite";
                  orgId = 1;
                }];
              }
            '';

            type = types.nullOr (
              types.submodule {
                options = {
                  apiVersion = mkOption {
                    default = 1;
                    description = "Config file version.";
                    type = types.int;
                  };

                  datasources = mkOption {
                    default = [ ];
                    description = "List of datasources to insert/update.";
                    type = types.listOf grafanaTypes.datasourceConfig;
                  };

                  deleteDatasources = mkOption {
                    default = [ ];
                    description = "List of datasources that should be deleted from the database.";

                    type = types.listOf (
                      types.submodule {
                        options.name = mkOption {
                          description = "Name of the datasource to delete.";
                          type = types.str;
                        };

                        options.orgId = mkOption {
                          description = "Organization ID of the datasource to delete.";
                          type = types.int;
                        };
                      }
                    );
                  };

                  prune = mkOption {
                    default = false;

                    description = ''
                      When `true`, provisioned datasources from this file will be deleted
                      automatically when removed from
                      {option}`services.grafana.provision.datasources.settings.datasources`.
                    '';

                    type = types.bool;
                  };
                };
              }
            );
          };
        };
      };
    };

    settings = mkOption {
      default = { };

      description = ''
        Grafana settings. See <https://grafana.com/docs/grafana/latest/setup-grafana/configure-grafana/>
        for available options. INI format is used.
      '';

      type = types.submodule {
        options = {
          analytics = {
            check_for_plugin_updates = mkOption {
              default = cfg.declarativePlugins == null;
              defaultText = literalExpression "cfg.declarativePlugins == null";

              description = ''
                When set to `false`, disables checking for new versions of installed plugins from https://grafana.com.
                When enabled, the check for a new plugin runs every 10 minutes.
                It will notify, via the UI, when a new plugin update exists.
                The check itself will not prompt any auto-updates of the plugin, nor will it send any sensitive information.
              '';

              type = types.bool;
            };

            check_for_updates = mkOption {
              default = false;

              description = ''
                When set to `false`, disables checking for new versions of Grafana from Grafana's GitHub repository.
                When enabled, the check for a new version runs every 10 minutes.
                It will notify, via the UI, when a new version is available.
                The check itself will not prompt any auto-updates of the Grafana software, nor will it send any sensitive information.
              '';

              type = types.bool;
            };

            feedback_links_enabled = mkOption {
              default = true;
              description = "Set to `false` to remove all feedback links from the UI.";
              type = types.bool;
            };

            reporting_enabled = mkOption {
              default = false;

              description = ''
                When enabled Grafana will send anonymous usage statistics to `stats.grafana.org`.
                No IP addresses are being tracked, only simple counters to track running instances, versions, dashboard and error counts.
                Counters are sent every 24 hours.
              '';

              type = types.bool;
            };
          };

          database = {
            ca_cert_path = mkOption {
              default = null;
              description = "The path to the CA certificate to use.";
              type = types.nullOr types.str;
            };

            cache_mode = mkOption {
              default = "private";

              description = ''
                For `sqlite3` only.
                [Shared cache](https://www.sqlite.org/sharedcache.html) setting used for connecting to the database.
              '';

              type = types.enum [
                "private"
                "shared"
              ];
            };

            client_cert_path = mkOption {
              default = null;
              description = "The path to the client cert. Only if server requires client authentication.";
              type = types.nullOr types.str;
            };

            client_key_path = mkOption {
              default = null;
              description = "The path to the client key. Only if server requires client authentication.";
              type = types.nullOr types.str;
            };

            conn_max_lifetime = mkOption {
              default = 14400;

              description = ''
                Sets the maximum amount of time a connection may be reused.
                The default is 14400 (which means 14400 seconds or 4 hours).
                For MySQL, this setting should be shorter than the `wait_timeout` variable.
              '';

              type = types.int;
            };

            host = mkOption {
              default = "127.0.0.1:3306";

              description = ''
                Only applicable to MySQL or Postgres.
                Includes IP or hostname and port or in case of Unix sockets the path to it.
                For example, for MySQL running on the same host as Grafana: `host = "127.0.0.1:3306"`
                or with Unix sockets: `host = "/var/run/mysqld/mysqld.sock"`
              '';

              type = types.str;
            };

            isolation_level = mkOption {
              default = null;

              description = ''
                Only the MySQL driver supports isolation levels in Grafana.
                In case the value is empty, the driver's default isolation level is applied.
              '';

              type = types.nullOr (
                types.enum [
                  "READ-UNCOMMITTED"
                  "READ-COMMITTED"
                  "REPEATABLE-READ"
                  "SERIALIZABLE"
                ]
              );
            };

            locking_attempt_timeout_sec = mkOption {
              default = 0;

              description = ''
                For `mysql`, if the `migrationLocking` feature toggle is set,
                specify the time (in seconds) to wait before failing to lock the database for the migrations.
              '';

              type = types.int;
            };

            log_queries = mkOption {
              default = false;
              description = "Set to `true` to log the sql calls and execution times";
              type = types.bool;
            };

            max_idle_conn = mkOption {
              default = 2;
              description = "The maximum number of connections in the idle connection pool.";
              type = types.int;
            };

            max_open_conn = mkOption {
              default = 0;
              description = "The maximum number of open connections to the database.";
              type = types.int;
            };

            name = mkOption {
              default = "grafana";
              description = "The name of the Grafana database.";
              type = types.str;
            };

            password = mkOption {
              default = "";

              description = ''
                The database user's password (not applicable for `sqlite3`).

                Please note that the contents of this option
                will end up in a world-readable Nix store. Use the file provider
                pointing at a reasonably secured file in the local filesystem
                to work around that. Look at the documentation for details:
                <https://grafana.com/docs/grafana/latest/setup-grafana/configure-grafana/#file-provider>
              '';

              type = types.str;
            };

            path = mkOption {
              default = "${cfg.dataDir}/data/grafana.db";
              defaultText = literalExpression ''"''${config.${opt.dataDir}}/data/grafana.db"'';
              description = "Only applicable to `sqlite3` database. The file path where the database will be stored.";
              type = types.path;
            };

            query_retries = mkOption {
              default = 0;

              description = ''
                This setting applies to `sqlite3` only and controls the number of times the system retries a query when the database is locked.
              '';

              type = types.int;
            };

            server_cert_name = mkOption {
              default = null;

              description = ''
                The common name field of the certificate used by the `mysql` or `postgres` server.
                Not necessary if `ssl_mode` is set to `skip-verify`.
              '';

              type = types.nullOr types.str;
            };

            ssl_mode = mkOption {
              default = "disable";

              description = ''
                For Postgres, use either `disable`, `require` or `verify-full`.
                For MySQL, use either `true`, `false`, or `skip-verify`.
              '';

              type = types.enum [
                "disable"
                "require"
                "verify-full"
                "true"
                "false"
                "skip-verify"
              ];
            };

            transaction_retries = mkOption {
              default = 5;

              description = ''
                This setting applies to `sqlite3` only and controls the number of times the system retries a transaction when the database is locked.
              '';

              type = types.int;
            };

            type = mkOption {
              default = "sqlite3";
              description = "Database type.";

              type = types.enum [
                "mysql"
                "sqlite3"
                "postgres"
              ];
            };

            user = mkOption {
              default = "root";
              description = "The database user (not applicable for `sqlite3`).";
              type = types.str;
            };

            wal = mkOption {
              default = false;

              description = ''
                For `sqlite3` only.
                Setting to enable/disable [Write-Ahead Logging](https://sqlite.org/wal.html).
              '';

              type = types.bool;
            };
            # TODO Add "instrument_queries" option when upgrading to grafana 10.0
            # instrument_queries = mkOption {
            #   description = "Set to `true` to add metrics and tracing for database queries.";
            #   default = false;
            #   type = types.bool;
            # };
          };

          paths = {
            plugins = mkOption {
              default = if (cfg.declarativePlugins == null) then "${cfg.dataDir}/plugins" else declarativePlugins;
              defaultText = literalExpression "if (cfg.declarativePlugins == null) then \"\${cfg.dataDir}/plugins\" else declarativePlugins";
              description = "Directory where grafana will automatically scan and look for plugins";
              type = types.path;
            };

            provisioning = mkOption {
              default = provisionConfDir;
              defaultText = "directory with links to files generated from services.grafana.provision";

              description = ''
                Folder that contains provisioning config files that grafana will apply on startup and while running.
                Don't change the value of this option if you are planning to use `services.grafana.provision` options.
              '';

              type = types.path;
            };
          };

          plugins = {
            preinstall_disabled = mkOption {
              default = cfg.declarativePlugins != null;
              defaultText = literalExpression "cfg.declarativePlugins != null";

              description = ''
                When set to `true`, disables the Background Plugin Installer, which runs before Grafana starts.
                This component causes issues with `declarativePlugins` and is disabled by default if those are used.
              '';

              type = types.bool;
            };
          };

          security = {
            admin_email = mkOption {
              default = "admin@localhost";
              description = "The email of the default Grafana Admin, created on startup.";
              type = types.str;
            };

            admin_password = mkOption {
              default = "admin";

              description = ''
                Default admin password. Please note that the contents of this option
                will end up in a world-readable Nix store. Use the file provider
                pointing at a reasonably secured file in the local filesystem
                to work around that. Look at the documentation for details:
                <https://grafana.com/docs/grafana/latest/setup-grafana/configure-grafana/#file-provider>
              '';

              type = types.str;
            };

            admin_user = mkOption {
              default = "admin";
              description = "Default admin username.";
              type = types.str;
            };

            allow_embedding = mkOption {
              default = false;

              description = ''
                When `false`, the HTTP header `X-Frame-Options: deny` will be set in Grafana HTTP responses
                which will instruct browsers to not allow rendering Grafana in a `<frame>`, `<iframe>`, `<embed>` or `<object>`.
                The main goal is to mitigate the risk of [Clickjacking](https://owasp.org/www-community/attacks/Clickjacking).
              '';

              type = types.bool;
            };

            content_security_policy = mkOption {
              default = false;

              description = ''
                Set to `true` to add the `Content-Security-Policy` header to your requests.
                CSP allows to control resources that the user agent can load and helps prevent XSS attacks.
              '';

              type = types.bool;
            };

            content_security_policy_report_only = mkOption {
              default = false;

              description = ''
                Set to `true` to add the `Content-Security-Policy-Report-Only` header to your requests.
                CSP in Report Only mode enables you to experiment with policies by monitoring their effects without enforcing them.
                You can enable both policies simultaneously.
              '';

              type = types.bool;
            };

            cookie_samesite = mkOption {
              default = "lax";

              description = ''
                Sets the `SameSite` cookie attribute and prevents the browser from sending this cookie along with cross-site requests.
                The main goal is to mitigate the risk of cross-origin information leakage.
                This setting also provides some protection against cross-site request forgery attacks (CSRF),
                [read more about SameSite here](https://owasp.org/www-community/SameSite).
                Using value `disabled` does not add any `SameSite` attribute to cookies.
              '';

              type = types.enum [
                "lax"
                "strict"
                "none"
                "disabled"
              ];
            };

            cookie_secure = mkOption {
              default = false;
              description = "Set to `true` if you host Grafana behind HTTPS.";
              type = types.bool;
            };

            csrf_additional_headers = mkOption {
              default = [ ];

              description = ''
                List of allowed headers to be set by the user.
                Suggested to use for if authentication lives behind reverse proxies.
              '';

              type = types.oneOf [
                types.str
                (types.listOf types.str)
              ];
            };

            # The options content_security_policy_template and
            # content_security_policy_template are missing because I'm not sure
            # how exactly the quoting of the default value works. See also
            # https://github.com/grafana/grafana/blob/cb7e18938b8eb6860a64b91aaba13a7eb31bc95b/conf/defaults.ini#L364
            # https://github.com/grafana/grafana/blob/cb7e18938b8eb6860a64b91aaba13a7eb31bc95b/conf/defaults.ini#L373
            # These two options are lists joined with spaces:
            # https://github.com/grafana/grafana/blob/916d9793aa81c2990640b55a15dee0db6b525e41/pkg/middleware/csrf/csrf.go#L37-L38
            csrf_trusted_origins = mkOption {
              default = [ ];

              description = ''
                List of additional allowed URLs to pass by the CSRF check.
                Suggested when authentication comes from an IdP.
              '';

              type = types.oneOf [
                types.str
                (types.listOf types.str)
              ];
            };

            data_source_proxy_whitelist = mkOption {
              default = [ ];

              description = ''
                Define a whitelist of allowed IP addresses or domains, with ports,
                to be used in data source URLs with the Grafana data source proxy.
                Format: `ip_or_domain:port` separated by spaces.
                PostgreSQL, MySQL, and MSSQL data sources do not use the proxy and are therefore unaffected by this setting.
              '';

              type = types.oneOf [
                types.str
                (types.listOf types.str)
              ];
            };

            disable_brute_force_login_protection = mkOption {
              default = false;
              description = "Set to `true` to disable [brute force login protection](https://cheatsheetseries.owasp.org/cheatsheets/Authentication_Cheat_Sheet.html#account-lockout).";
              type = types.bool;
            };

            disable_gravatar = mkOption {
              default = false;
              description = "Set to `true` to disable the use of Gravatar for user profile images.";
              type = types.bool;
            };

            disable_initial_admin_creation = mkOption {
              default = false;
              description = "Disable creation of admin user on first start of Grafana.";
              type = types.bool;
            };

            secret_key = mkOption {
              default = null;

              description = ''
                Secret key used for signing data source settings like secrets and passwords.
                Set this to a unique, random string in production, generated for example by running `openssl rand -hex 32`.

                If you change this later you will need to update data source settings to re-encode them.

                <https://grafana.com/docs/grafana/latest/setup-grafana/configure-grafana/#secret_key>

                Please note that the contents of this option
                will end up in a world-readable Nix store. Use the file provider
                pointing at a reasonably secured file in the local filesystem
                to work around that. Look at the documentation for details:
                <https://grafana.com/docs/grafana/latest/setup-grafana/configure-grafana/#file-provider>
              '';

              type = types.nullOr types.str;
            };

            strict_transport_security = mkOption {
              default = false;

              description = ''
                Set to `true` if you want to enable HTTP `Strict-Transport-Security` (HSTS) response header.
                Only use this when HTTPS is enabled in your configuration,
                or when there is another upstream system that ensures your application does HTTPS (like a frontend load balancer).
                HSTS tells browsers that the site should only be accessed using HTTPS.
              '';

              type = types.bool;
            };

            strict_transport_security_max_age_seconds = mkOption {
              default = 86400;

              description = ''
                Sets how long a browser should cache HSTS in seconds.
                Only applied if `strict_transport_security` is enabled.
              '';

              type = types.int;
            };

            strict_transport_security_preload = mkOption {
              default = false;

              description = ''
                Set to `true` to enable HSTS `preloading` option.
                Only applied if `strict_transport_security` is enabled.
              '';

              type = types.bool;
            };

            strict_transport_security_subdomains = mkOption {
              default = false;

              description = ''
                Set to `true` to enable HSTS `includeSubDomains` option.
                Only applied if `strict_transport_security` is enabled.
              '';

              type = types.bool;
            };

            x_content_type_options = mkOption {
              default = true;

              description = ''
                Set to `false` to disable the `X-Content-Type-Options` response header.
                The `X-Content-Type-Options` response HTTP header is a marker used by the server
                to indicate that the MIME types advertised in the `Content-Type` headers should not be changed and be followed.
              '';

              type = types.bool;
            };

            x_xss_protection = mkOption {
              default = false;

              description = ''
                Set to `true` to enable the `X-XSS-Protection` header,
                which tells browsers to stop pages from loading when they detect reflected cross-site scripting (XSS) attacks.

                __Note:__ this is the default in Grafana, it's turned off here
                since it's [recommended to not use this header anymore](https://owasp.org/www-project-secure-headers/#x-xss-protection).
              '';

              type = types.bool;
            };
          };

          server = {
            cdn_url = mkOption {
              default = null;

              description = ''
                Specify a full HTTP URL address to the root of your Grafana CDN assets.
                Grafana will add edition and version paths.

                For example, given a cdn url like `https://cdn.myserver.com`
                grafana will try to load a javascript file from `http://cdn.myserver.com/grafana-oss/7.4.0/public/build/app.<hash>.js`.
              '';

              type = types.nullOr types.str;
            };

            cert_file = mkOption {
              default = null;

              description = ''
                Path to the certificate file (if `protocol` is set to `https` or `h2`).
              '';

              type = types.nullOr types.str;
            };

            cert_key = mkOption {
              default = null;

              description = ''
                Path to the certificate key file (if `protocol` is set to `https` or `h2`).
              '';

              type = types.nullOr types.str;
            };

            domain = mkOption {
              default = "localhost";

              description = ''
                The public facing domain name used to access grafana from a browser.

                This setting is only used in the default value of the `root_url` setting.
                If you set the latter manually, this option does not have to be specified.
              '';

              type = types.str;
            };

            enable_gzip = mkOption {
              default = false;

              description = ''
                Set this option to `true` to enable HTTP compression, this can improve transfer speed and bandwidth utilization.
                It is recommended that most users set it to `true`. By default it is set to `false` for compatibility reasons.
              '';

              type = types.bool;
            };

            enforce_domain = mkOption {
              default = false;

              description = ''
                Redirect to correct domain if the host header does not match the domain.
                Prevents DNS rebinding attacks.
              '';

              type = types.bool;
            };

            http_addr = mkOption {
              default = "127.0.0.1";

              description = ''
                Listening address.

                ::: {.note}
                This setting intentionally varies from upstream's default to be a bit more secure by default.
                :::
              '';

              type = types.str;
            };

            http_port = mkOption {
              default = 3000;
              description = "Listening port.";
              type = types.port;
            };

            protocol = mkOption {
              default = "http";
              description = "Which protocol to listen.";

              type = types.enum [
                "http"
                "https"
                "h2"
                "socket"
              ];
            };

            read_timeout = mkOption {
              default = "0";

              description = ''
                Sets the maximum time using a duration format (5s/5m/5ms)
                before timing out read of an incoming request and closing idle connections.
                0 means there is no timeout for reading the request.
              '';

              type = types.str;
            };

            root_url = mkOption {
              default = "%(protocol)s://%(domain)s:%(http_port)s/";

              description = ''
                This is the full URL used to access Grafana from a web browser.
                This is important if you use Google or GitHub OAuth authentication (for the callback URL to be correct).

                This setting is also important if you have a reverse proxy in front of Grafana that exposes it through a subpath.
                In that case add the subpath to the end of this URL setting.
              '';

              type = types.str;
            };

            router_logging = mkOption {
              default = false;

              description = ''
                Set to `true` for Grafana to log all HTTP requests (not just errors).
                These are logged as Info level events to the Grafana log.
              '';

              type = types.bool;
            };

            serve_from_sub_path = mkOption {
              default = false;

              description = ''
                Serve Grafana from subpath specified in the `root_url` setting.
                By default it is set to `false` for compatibility reasons.

                By enabling this setting and using a subpath in `root_url` above,
                e.g. `root_url = "http://localhost:3000/grafana"`,
                Grafana is accessible on `http://localhost:3000/grafana`.
                If accessed without subpath, Grafana will redirect to an URL with the subpath.
              '';

              type = types.bool;
            };

            socket = mkOption {
              default = "/run/grafana/grafana.sock";

              description = ''
                Path where the socket should be created when `protocol=socket`.
                Make sure that Grafana has appropriate permissions before you change this setting.
              '';

              type = types.str;
            };

            socket_gid = mkOption {
              default = -1;

              description = ''
                GID where the socket should be set when `protocol=socket`.
                Make sure that the target group is in the group of Grafana process and that Grafana process is the file owner before you change this setting.
                It is recommended to set the gid as http server user gid.
                Not set when the value is -1.
              '';

              type = types.int;
            };

            socket_mode = mkOption {
              # I assume this value is interpreted as octal literal by grafana.
              # If this was an int, people following tutorials or porting their
              # old config could stumble across nix not having octal literals.
              default = "0660";

              description = ''
                Mode where the socket should be set when `protocol=socket`.
                Make sure that Grafana process is the file owner before you change this setting.
              '';

              type = types.str;
            };

            static_root_path = mkOption {
              default = "${cfg.package}/share/grafana/public";
              defaultText = literalExpression ''"''${package}/share/grafana/public"'';
              description = "Root path for static assets.";
              type = types.str;
            };
          };

          smtp = {
            cert_file = mkOption {
              default = null;
              description = "File path to a cert file.";
              type = types.nullOr types.str;
            };

            ehlo_identity = mkOption {
              default = null;
              description = "Name to be used as client identity for EHLO in SMTP dialog.";
              type = types.nullOr types.str;
            };

            enabled = mkOption {
              default = false;
              description = "Whether to enable SMTP.";
              type = types.bool;
            };

            from_address = mkOption {
              default = "admin@grafana.localhost";
              description = "Address used when sending out emails.";
              type = types.str;
            };

            from_name = mkOption {
              default = "Grafana";
              description = "Name to be used as client identity for EHLO in SMTP dialog.";
              type = types.str;
            };

            host = mkOption {
              default = "localhost:25";
              description = "Host to connect to.";
              type = types.str;
            };

            key_file = mkOption {
              default = null;
              description = "File path to a key file.";
              type = types.nullOr types.str;
            };

            password = mkOption {
              default = "";

              description = ''
                Password used for authentication. Please note that the contents of this option
                will end up in a world-readable Nix store. Use the file provider
                pointing at a reasonably secured file in the local filesystem
                to work around that. Look at the documentation for details:
                <https://grafana.com/docs/grafana/latest/setup-grafana/configure-grafana/#file-provider>
              '';

              type = types.str;
            };

            skip_verify = mkOption {
              default = false;
              description = "Verify SSL for SMTP server.";
              type = types.bool;
            };

            startTLS_policy = mkOption {
              default = null;
              description = "StartTLS policy when connecting to server.";

              type = types.nullOr (
                types.enum [
                  "OpportunisticStartTLS"
                  "MandatoryStartTLS"
                  "NoStartTLS"
                ]
              );
            };

            user = mkOption {
              default = null;
              description = "User used for authentication.";
              type = types.nullOr types.str;
            };
          };

          users = {
            allow_org_create = mkOption {
              default = false;
              description = "Set to `false` to prohibit users from creating new organizations.";
              type = types.bool;
            };

            allow_sign_up = mkOption {
              default = false;

              description = ''
                Set to false to prohibit users from being able to sign up / create user accounts.
                The admin user can still create users.
              '';

              type = types.bool;
            };

            auto_assign_org = mkOption {
              default = true;

              description = ''
                Set to `true` to automatically add new users to the main organization (id 1).
                When set to `false,` new users automatically cause a new organization to be created for that new user.
                The organization will be created even if the `allow_org_create` setting is set to `false`.
              '';

              type = types.bool;
            };

            auto_assign_org_id = mkOption {
              default = 1;

              description = ''
                Set this value to automatically add new users to the provided org.
                This requires `auto_assign_org` to be set to `true`.
                Please make sure that this organization already exists.
              '';

              type = types.int;
            };

            auto_assign_org_role = mkOption {
              default = "Viewer";

              description = ''
                The role new users will be assigned for the main organization (if the `auto_assign_org` setting is set to `true`).
              '';

              type = types.enum [
                "Viewer"
                "Editor"
                "Admin"
              ];
            };

            default_language = mkOption {
              default = "en-US";
              description = "This setting configures the default UI language, which must be a supported IETF language tag, such as `en-US`.";
              type = types.str;
            };

            default_theme = mkOption {
              default = "dark";
              description = "Sets the default UI theme. `system` matches the user's system theme.";

              type = types.enum [
                "dark"
                "light"
                "system"
              ];
            };

            # Lists are joined via space, so this option can't be a list.
            # Users have to manually join their values.
            hidden_users = mkOption {
              default = "";

              description = ''
                This is a comma-separated list of usernames.
                Users specified here are hidden in the Grafana UI.
                They are still visible to Grafana administrators and to themselves.
              '';

              type = types.str;
            };

            home_page = mkOption {
              default = "";

              description = ''
                Path to a custom home page.
                Users are only redirected to this if the default home dashboard is used.
                It should match a frontend route and contain a leading slash.
              '';

              type = types.str;
            };

            login_hint = mkOption {
              default = "email or username";
              description = "Text used as placeholder text on login page for login/username input.";
              type = types.str;
            };

            password_hint = mkOption {
              default = "password";
              description = "Text used as placeholder text on login page for password input.";
              type = types.str;
            };

            user_invite_max_lifetime_duration = mkOption {
              default = "24h";

              description = ''
                The duration in time a user invitation remains valid before expiring.
                This setting should be expressed as a duration.
                Examples: `6h` (hours), `2d` (days), `1w` (week).
                The minimum supported duration is `15m` (15 minutes).
              '';

              type = types.str;
            };

            verify_email_enabled = mkOption {
              default = false;
              description = "Require email validation before sign up completes.";
              type = types.bool;
            };

            viewers_can_edit = mkOption {
              default = false;

              description = ''
                Viewers can access and use Explore and perform temporary edits on panels in dashboards they have access to.
                They cannot save their changes.
              '';

              type = types.bool;
            };
          };
        };

        freeformType = settingsFormatIni.type;
      };
    };
  };

  config = mkIf cfg.enable {
    assertions = [
      {
        assertion = !(cfg.settings.users ? editors_can_admin);

        message = ''
          Option `services.grafana.settings.users.editors_can_admin` has been removed in Grafana 12.
        '';
      }
      {
        assertion = cfg.provision.datasources.settings == null || cfg.provision.datasources.path == null;
        message = "Cannot set both datasources settings and datasources path";
      }
      {
        assertion =
          let
            prometheusIsNotDirect =
              opt: all ({ access, type, ... }: type == "prometheus" -> access != "direct") opt;
          in
          cfg.provision.datasources.settings == null
          || prometheusIsNotDirect cfg.provision.datasources.settings.datasources;

        message = "For datasources of type `prometheus`, the `direct` access mode is not supported anymore (since Grafana 9.2.0)";
      }
      {
        assertion = cfg.provision.dashboards.settings == null || cfg.provision.dashboards.path == null;
        message = "Cannot set both dashboards settings and dashboards path";
      }
      {
        assertion =
          cfg.provision.alerting.rules.settings == null || cfg.provision.alerting.rules.path == null;

        message = "Cannot set both rules settings and rules path";
      }
      {
        assertion =
          cfg.provision.alerting.contactPoints.settings == null
          || cfg.provision.alerting.contactPoints.path == null;

        message = "Cannot set both contact points settings and contact points path";
      }
      {
        assertion =
          cfg.provision.alerting.policies.settings == null || cfg.provision.alerting.policies.path == null;

        message = "Cannot set both policies settings and policies path";
      }
      {
        assertion =
          cfg.provision.alerting.templates.settings == null || cfg.provision.alerting.templates.path == null;

        message = "Cannot set both templates settings and templates path";
      }
      {
        assertion =
          cfg.provision.alerting.muteTimings.settings == null
          || cfg.provision.alerting.muteTimings.path == null;

        message = "Cannot set both mute timings settings and mute timings path";
      }
      {
        assertion = cfg.settings.security.secret_key != null;

        message = ''
          Grafana's secret key (services.grafana.settings.security.secret_key) doesn't have a default
          value anymore. Please generate your own and use a file-provider on this option! See also
          https://grafana.com/docs/grafana/latest/setup-grafana/configure-grafana/#secret_key
          for more information.

          See https://grafana.com/docs/grafana/latest/setup-grafana/configure-security/configure-database-encryption/#re-encrypt-secrets on how to re-encrypt.

          As stated in the NixOS changelog for 26.05, there's no official way to rotate.
          Either hard-code the old key ("SW2YcwTIb9zpOOhoPsMm") if your setup doesn't have any secrets in the DB that need
          special protection or perform a rotation with a 3rd-party tool
          (https://github.com/erooke/grafana-secretkey-rotation-tool/tree/d9dc788902fa5185e15cb15ce6129f7237ab6138).
        '';
      }
    ];

    environment.systemPackages = [ cfg.package ];
    networking.firewall.allowedTCPPorts = mkIf cfg.openFirewall [ cfg.settings.server.http_port ];

    systemd.services.grafana = {
      after = [
        "network.target"
      ]
      ++ lib.optional usePostgresql "postgresql.target"
      ++ lib.optional useMysql "mysql.service";

      description = "Grafana Service Daemon";

      preStart = ''
        ln -fs ${cfg.package}/share/grafana/conf ${cfg.dataDir}
        ln -fs ${cfg.package}/share/grafana/tools ${cfg.dataDir}
      '';

      script = ''
        set -o errexit -o pipefail -o nounset -o errtrace
        shopt -s inherit_errexit

        exec ${lib.getExe cfg.package} server -homepath ${cfg.dataDir} -config ${configFile}
      '';

      serviceConfig = {
        # Hardening
        AmbientCapabilities = lib.mkIf (cfg.settings.server.http_port < 1024) [ "CAP_NET_BIND_SERVICE" ];

        CapabilityBoundingSet =
          if (cfg.settings.server.http_port < 1024) then [ "CAP_NET_BIND_SERVICE" ] else [ "" ];

        DeviceAllow = [ "" ];
        LockPersonality = true;
        NoNewPrivileges = true;
        PrivateDevices = true;
        PrivateTmp = true;
        ProtectClock = true;
        ProtectControlGroups = true;
        ProtectHome = true;
        ProtectHostname = true;
        ProtectKernelLogs = true;
        ProtectKernelModules = true;
        ProtectKernelTunables = true;
        ProtectProc = "invisible";
        ProtectSystem = "full";
        RemoveIPC = true;
        Restart = "on-failure";

        RestrictAddressFamilies = [
          "AF_INET"
          "AF_INET6"
          "AF_UNIX"
        ];

        RestrictNamespaces = true;
        RestrictRealtime = true;
        RestrictSUIDSGID = true;
        RuntimeDirectory = "grafana";
        RuntimeDirectoryMode = "0755";
        SystemCallArchitectures = "native";

        # Upstream grafana is not setting SystemCallFilter for compatibility
        # reasons, see https://github.com/grafana/grafana/pull/40176
        SystemCallFilter = [
          "@system-service"
          "~@privileged"
        ]
        ++ lib.optionals (cfg.settings.server.protocol == "socket") [ "@chown" ];

        UMask = "0027";
        User = "grafana";
        WorkingDirectory = cfg.dataDir;
      };

      wantedBy = [ "multi-user.target" ];
    };

    users.groups.grafana = { };

    users.users.grafana = {
      createHome = true;
      description = "Grafana user";
      group = "grafana";
      home = cfg.dataDir;
      uid = config.ids.uids.grafana;
    };

    warnings =
      let
        doesntUseFileProvider =
          opt: defaultValue:
          let
            regex = "${
              optionalString (defaultValue != null) "^${defaultValue}$|"
            }^\\$__(file|env)\\{.*}$|^\\$[^_\\$][^ ]+$";
          in
          builtins.match regex opt == null;

        # Ensure that no custom credentials are leaked into the Nix store. Unless the default value
        # is specified, this can be achieved by using the file/env provider:
        # https://grafana.com/docs/grafana/latest/setup-grafana/configure-grafana/#variable-expansion
        passwordWithoutFileProvider =
          optional
            (
              doesntUseFileProvider cfg.settings.database.password ""
              || doesntUseFileProvider cfg.settings.security.admin_password "admin"
            )
            ''
              Grafana passwords will be stored as plaintext in the Nix store!
              Use file provider or an env-var instead.
            '';

        # Ensure that `secureJsonData` of datasources provisioned via `datasources.settings`
        # only uses file/env providers.
        secureJsonDataWithoutFileProvider =
          optional
            (
              let
                datasourcesToCheck = optionals (
                  cfg.provision.datasources.settings != null
                ) cfg.provision.datasources.settings.datasources;
                declarationUnsafe =
                  { secureJsonData, ... }:
                  secureJsonData != null && any (flip doesntUseFileProvider null) (attrValues secureJsonData);
              in
              any declarationUnsafe datasourcesToCheck
            )
            ''
              Declarations in the `secureJsonData`-block of a datasource will be leaked to the
              Nix store unless a file-provider or an env-var is used!
            '';
      in
      passwordWithoutFileProvider ++ secureJsonDataWithoutFileProvider;
  };
}
