{
  config,
  lib,
  pkgs,
  ...
}:

let
  inherit (pkgs) writeScript;

  pkgs2storeContents = map (x: {
    object = x;
    symlink = "none";
  });
in

{
  # Docker image config.
  imports = [
    ../installer/cd-dvd/channel.nix
    ./minimal.nix
    ./clone-config.nix
  ];

  boot.isContainer = true;

  # Update /init symlink when switching configurations so the container
  # boots the new system on restart.
  system.build.installBootLoader = pkgs.writeShellScript "install-docker-init" ''
    ${pkgs.coreutils}/bin/ln -fs "$1/init" /init
  '';

  # Create the tarball
  system.build.tarball = pkgs.callPackage ../../lib/make-system-tarball.nix {
    contents = [
      {
        source = "${config.system.build.toplevel}/.";
        target = "./";
      }
    ];

    extraArgs = "--owner=0";

    # Some container managers like lxc need these
    extraCommands =
      let
        script = writeScript "extra-commands.sh" ''
          rm etc
          mkdir -p proc sys dev etc
        '';
      in
      script;

    # Add init script to image
    storeContents = pkgs2storeContents [
      config.system.build.toplevel
      pkgs.stdenv
    ];
  };

  systemd.services.register-nix-paths = {
    after = [ "local-fs.target" ];

    before = [
      "sysinit.target"
      "shutdown.target"
      "nix-daemon.socket"
      "nix-daemon.service"
    ];

    conflicts = [ "shutdown.target" ];
    description = "Register Nix Store Paths";
    restartIfChanged = false;

    script = ''
      ${lib.getExe' config.nix.package.out "nix-store"} --load-db < /nix-path-registration
      rm /nix-path-registration

      # nixos-rebuild also requires a "system" profile
      ${lib.getExe' config.nix.package.out "nix-env"} -p /nix/var/nix/profiles/system --set /run/current-system
    '';

    serviceConfig = {
      RemainAfterExit = true;
      Type = "oneshot";
    };

    unitConfig = {
      ConditionPathExists = "/nix-path-registration";
      DefaultDependencies = false;
    };

    wantedBy = [ "sysinit.target" ];
  };
}
