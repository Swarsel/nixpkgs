{
  lib,
  stdenv,
  fetchurl,
  gitUpdater,
  meson,
  ninja,
  pipewire,
}:

stdenv.mkDerivation (finalAttrs: {
  pname = "lv2";
  version = "1.18.10";

  src = fetchurl {
    url = "https://lv2plug.in/spec/lv2-${finalAttrs.version}.tar.xz";
    hash = "sha256-eMUbzyG1Tli7Yymsy7Ta4Dsu15tSD5oB5zS9neUwlT8=";
  };

  outputs = [
    "out"
    "dev"
  ];

  strictDeps = true;

  nativeBuildInputs = [
    meson
    ninja
  ];

  mesonFlags = [
    # install validators to $dev
    "--bindir=${placeholder "dev"}/bin"

    # These are just example plugins. They pull in outdated gtk-2
    # dependency and many other things. Upstream would like to
    # eventually move them of the project:
    #   https://gitlab.com/lv2/lv2/-/issues/57#note_1096060029
    "-Dplugins=disabled"
    # Pulls in spell checkers among other things.
    "-Dtests=disabled"
    # Avoid heavyweight python dependencies.
    "-Ddocs=disabled"
  ]
  ++ lib.optionals stdenv.hostPlatform.isDarwin [
    "-Dlv2dir=${placeholder "out"}/lib/lv2"
  ];

  passthru = {
    tests = {
      inherit pipewire;
    };

    updateScript = gitUpdater {
      rev-prefix = "v";
      # No nicer place to find latest release.
      url = "https://gitlab.com/lv2/lv2.git";
    };
  };

  meta = {
    description = "Plugin standard for audio systems";
    homepage = "https://lv2plug.in";
    license = lib.licenses.mit;
    maintainers = [ ];
    platforms = lib.platforms.unix;
    mainProgram = "lv2_validate";
  };
})
