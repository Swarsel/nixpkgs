{
  lib,
  fetchFromGitHub,
  gdk-pixbuf,
  gitUpdater,
  glib,
  gobject-introspection,
  gtk3,
  intltool,
  libmatekbd,
  libnotify,
  marco,
  mate-applets,
  mate-panel,
  mate-session-manager,
  python3Packages,
  wrapGAppsHook3,
}:

python3Packages.buildPythonApplication rec {
  pname = "mate-tweak";
  version = "22.10.0";

  src = fetchFromGitHub {
    owner = "ubuntu-mate";
    repo = "mate-tweak";
    rev = version;
    sha256 = "emeNgCzMhHMeLOyUkXe+8OzQMEWuwNdD4xkGXIFgbh4=";
  };

  postPatch = ''
    # mate-tweak hardcodes absolute paths everywhere. Nuke from orbit.
    find . -type f -exec sed -i \
      -e s,/usr/lib/mate-tweak,$out/lib/mate-tweak,g \
      {} +

    sed -i 's,{prefix}/,,g' setup.py
  '';

  strictDeps = false;

  nativeBuildInputs = [
    wrapGAppsHook3
    intltool
    python3Packages.distutils-extra
    gobject-introspection
  ];

  buildInputs = [
    gtk3
    gdk-pixbuf
    libnotify
    glib
    mate-applets
    mate-panel
    marco
    libmatekbd
    mate-session-manager
  ];

  propagatedBuildInputs = with python3Packages; [
    distro
    pygobject3
    psutil
    setproctitle
  ];

  # Arguments to be passed to `makeWrapper`, only used by buildPython*
  preFixup = ''
    makeWrapperArgs+=("''${gappsWrapperArgs[@]}")
  '';

  postFixup = ''
    for i in bin/.mate-tweak-wrapped lib/mate-tweak/mate-tweak-helper; do
      sed -i "s,usr,run/current-system/sw,g" $out/$i
    done
  '';

  dontWrapGApps = true;
  format = "setuptools";
  passthru.updateScript = gitUpdater { };

  meta = {
    description = "Tweak tool for the MATE Desktop";
    homepage = "https://github.com/ubuntu-mate/mate-tweak";
    changelog = "https://github.com/ubuntu-mate/mate-tweak/releases/tag/${version}";
    license = [ lib.licenses.gpl2Plus ];
    platforms = lib.platforms.linux;
    teams = [ lib.teams.mate ];
  };
}
