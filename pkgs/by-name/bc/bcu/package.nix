{
  lib,
  stdenv,
  fetchFromGitHub,
  cmake,
  libftdi1,
  libusb1,
  libyaml,
  ncurses,
  nix-update-script,
  pkg-config,
}:

stdenv.mkDerivation (finalAttrs: {
  pname = "bcu";
  version = "1.1.128";

  src = fetchFromGitHub {
    owner = "nxp-imx";
    repo = "bcu";
    tag = "bcu_${finalAttrs.version}";
    hash = "sha256-8q9xJYEZfyC8ETNi3q8YQOtBGMmI4EQLp7LKxPaU65Q=";
  };

  patches = [ ./darwin-install.patch ];

  postPatch = ''
    substituteInPlace create_version_h.sh \
      --replace-fail "version=\`git describe --tags --long\`" "version=${finalAttrs.src.tag}"
  '';

  strictDeps = true;

  nativeBuildInputs = [
    cmake
    pkg-config
  ];

  buildInputs = [
    libftdi1
    libusb1
    libyaml
    ncurses
  ];

  env.NIX_CFLAGS_COMPILE = "-Wno-pointer-sign -Wno-deprecated-declarations -Wno-switch";

  preFixup = lib.optionalString stdenv.hostPlatform.isDarwin ''
    ln -sf $out/bin/bcu_mac $out/bin/bcu
  '';

  enableParallelBuilding = true;
  passthru.updateScript = nix-update-script { };

  meta = {
    description = "NXP i.MX remote control and power measurement tools";
    homepage = "https://github.com/nxp-imx/bcu";
    license = lib.licenses.bsd3;
    maintainers = [ lib.maintainers.jmbaur ];
    platforms = lib.platforms.linux ++ lib.platforms.darwin;
    mainProgram = "bcu";
  };
})
