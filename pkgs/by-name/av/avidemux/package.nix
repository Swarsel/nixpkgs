{
  lib,
  stdenv,
  fetchurl,
  alsa-lib,
  cmake,
  faac,
  faad2,
  fetchpatch,
  fontconfig,
  freetype,
  fribidi,
  gettext,
  lame,
  libGLU,
  libopus,
  libpulseaudio,
  libsForQt5,
  libva,
  libvdpau,
  libvorbis,
  libvpx,
  libxext,
  libxv,
  makeWrapper,
  pkg-config,
  sqlite,
  x264,
  x265,
  xvidcore,
  yasm,
  zlib,
  default ? "qt5",
  withCLI ? true,
  withFAAC ? false,
  withFAAD ? true,
  withLAME ? true,
  withOpus ? true,
  withPlugins ? true,
  withPulse ? true,
  withQT ? true,
  withVPX ? true,
  withVorbis ? true,
  withX264 ? true,
  withX265 ? true,
  withXvid ? true,
}:

assert default != "qt5" -> default == "cli";
assert !withQT -> default != "qt5";

stdenv.mkDerivation (finalAttrs: {
  pname = "avidemux";
  version = "2.8.1";

  src = fetchurl {
    url = "mirror://sourceforge/avidemux/avidemux/${finalAttrs.version}/avidemux_${finalAttrs.version}.tar.gz";
    sha256 = "sha256-d9m9yoaDzlfBkradIHz6t8+Sp3Wc4PY/o3tcjkKtPaI=";
  };

  patches = [
    ./dynamic_install_dir.patch
    ./bootstrap_logging.patch
    # x265 API change in 4.1 breaks build
    # See discussion in https://avidemux.org/smif/index.php/topic,19995.msg97494.html#msg97494
    (fetchpatch {
      hash = "sha256-5QqocvYaY/phyvSX2lhTzeAi+z9Wgqs+ITR0cXReps4=";
      name = "fix_build_with_x265_4_1.patch";
      url = "https://github.com/mean00/avidemux2/commit/c16d32a67cdb012db093472ad3776713939a30d1.patch";
    })
  ];

  postPatch = ''
    cp ${
      fetchpatch {
        hash = "sha256-s9PcYbt0mFb2wvgMcFL1J+2OS6Sxyd2wYkGzLr2qd9M=";
        # Backport fix for binutils-2.41.
        name = "binutils-2.41.patch";
        stripLen = 1;
        url = "https://git.ffmpeg.org/gitweb/ffmpeg.git/patch/effadce6c756247ea8bae32dc13bb3e6f464f0eb";
      }
    } avidemux_core/ffmpeg_package/patches/

    # Those CMake versions are deprecated and is no longer supported by CMake > 4
    # https://github.com/NixOS/nixpkgs/issues/445447
    substituteInPlace {avidemux_plugins,avidemux_core,avidemux/{cli,qt4}}/CMakeLists.txt --replace-fail \
      "cmake_minimum_required(VERSION 3.0)" \
      "cmake_minimum_required(VERSION 3.10)"
    substituteInPlace avidemux/gtk/CMakeLists.txt --replace-fail \
      "cmake_minimum_required(VERSION 2.6)" \
      "cmake_minimum_required(VERSION 3.10)"
    substituteInPlace avidemux/qt4/xdg_data/CMakeLists.txt --replace-fail \
      "cmake_minimum_required(VERSION 2.8.11)" \
      "cmake_minimum_required(VERSION 3.10)"
    substituteInPlace addons/fontGen/CMakeLists.txt --replace-fail \
      "cmake_minimum_required(VERSION 2.8)" \
      "cmake_minimum_required(VERSION 3.10)"
  '';

  nativeBuildInputs = [
    yasm
    cmake
    pkg-config
    makeWrapper
  ]
  ++ lib.optional withQT libsForQt5.wrapQtAppsHook;

  buildInputs = [
    zlib
    gettext
    libvdpau
    libva
    libxv
    sqlite
    fribidi
    fontconfig
    freetype
    alsa-lib
    libxext
    libGLU
  ]
  ++ lib.optional withX264 x264
  ++ lib.optional withX265 x265
  ++ lib.optional withXvid xvidcore
  ++ lib.optional withLAME lame
  ++ lib.optional withFAAC faac
  ++ lib.optional withVorbis libvorbis
  ++ lib.optional withPulse libpulseaudio
  ++ lib.optional withFAAD faad2
  ++ lib.optional withOpus libopus
  ++ lib.optionals withQT (
    with libsForQt5;
    [
      qttools
      qtbase
    ]
  )
  ++ lib.optional withVPX libvpx;

  buildCommand =
    let
      wrapWith =
        makeWrapper: filename:
        "${makeWrapper} ${filename} --set ADM_ROOT_DIR $out --prefix LD_LIBRARY_PATH : ${libxext}/lib";
      wrapQtApp = wrapWith "wrapQtApp";
      wrapProgram = wrapWith "wrapProgram";
    in
    ''
      unpackPhase
      cd "$sourceRoot"
      patchPhase

      ${stdenv.shell} bootStrap.bash \
        --with-core \
        ${if withQT then "--with-qt" else "--without-qt"} \
        ${if withCLI then "--with-cli" else "--without-cli"} \
        ${if withPlugins then "--with-plugins" else "--without-plugins"}

      mkdir $out
      cp -R install/usr/* $out

      ${wrapProgram "$out/bin/avidemux3_cli"}

      ${lib.optionalString withQT ''
        ${wrapQtApp "$out/bin/avidemux3_qt5"}
        ${wrapQtApp "$out/bin/avidemux3_jobs_qt5"}
      ''}

      ln -s "$out/bin/avidemux3_${default}" "$out/bin/avidemux"

      # make the install path match the rpath
      if [[ -d ''${!outputLib}/lib64 ]]; then
        mv ''${!outputLib}/lib64 ''${!outputLib}/lib
        ln -s lib ''${!outputLib}/lib64
      fi
      fixupPhase
    '';

  dontWrapQtApps = true;

  meta = {
    description = "Free video editor designed for simple video editing tasks";
    homepage = "http://fixounet.free.fr/avidemux/";
    license = lib.licenses.gpl2;
    maintainers = [ ];

    # "CPU not supported" errors on AArch64
    platforms = [
      "i686-linux"
      "x86_64-linux"
    ];
  };
})
