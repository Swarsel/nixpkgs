{
  lib,
  stdenv,
  fetchurl,
  fixDarwinDylibNames,
  validatePkgConfig,
}:

stdenv.mkDerivation (finalAttrs: {
  pname = "duktape";
  version = "2.7.0";

  src = fetchurl {
    url = "https://duktape.org/duktape-${finalAttrs.version}.tar.xz";
    sha256 = "sha256-kPjS+otVZ8aJmDDd7ywD88J5YLEayiIvoXqnrGE8KJA=";
  };

  nativeBuildInputs = [
    validatePkgConfig
  ]
  ++ lib.optionals stdenv.hostPlatform.isDarwin [ fixDarwinDylibNames ];

  # https://github.com/svaarala/duktape/issues/2464
  env.LDFLAGS = "-lm";

  buildPhase = ''
    make -f Makefile.cmdline
  ''
  + lib.optionalString (!stdenv.hostPlatform.isStatic) ''
    make INSTALL_PREFIX="$out" -f Makefile.sharedlibrary
  '';

  installPhase = ''
    install -d $out/bin
    install -m755 duk $out/bin/
  ''
  + lib.optionalString (!stdenv.hostPlatform.isStatic) ''
    install -d $out/lib/pkgconfig
    install -d $out/include

    make INSTALL_PREFIX="$out" -f Makefile.sharedlibrary install
  '';

  enableParallelBuilding = true;

  meta = {
    description = "Embeddable Javascript engine, with a focus on portability and compact footprint";
    homepage = "https://duktape.org/";
    license = lib.licenses.mit;
    maintainers = [ lib.maintainers.fgaz ];
    platforms = lib.platforms.all;
    mainProgram = "duk";
    downloadPage = "https://duktape.org/download.html";
  };
})
