{
  lib,
  stdenv,
  fetchFromGitHub,
  testers,
  uasm,
}:

stdenv.mkDerivation (finalAttrs: {
  pname = "uasm";
  version = "2.57";

  src = fetchFromGitHub {
    owner = "Terraspace";
    repo = "UASM";
    tag = "v${finalAttrs.version}r";
    hash = "sha256-HaiK2ogE71zwgfhWL7fesMrNZYnh8TV/kE3ZIS0l85w=";
  };

  outputs = [
    "out"
    "doc"
  ];

  postPatch = ''
    substituteInPlace Makefile-DOS-GCC.mak \
      --replace-fail "gcc.exe" "${stdenv.cc.targetPrefix}cc"

    substituteInPlace Makefile-Linux-GCC-64.mak \
      --replace-fail "CC = gcc" "CC=${stdenv.cc.targetPrefix}cc"
  '';

  # Needed for compiling with GCC > 13
  env.NIX_CFLAGS_COMPILE = toString [
    "-std=c99"
    "-Wno-incompatible-pointer-types"
    "-Wno-int-conversion"
    "-Wno-implicit-function-declaration"
  ];

  installPhase = ''
    runHook preInstall

    ${
      if stdenv.hostPlatform.isWindows then
        ''
          install -Dm0755 DJGPPr/hjwasm.exe "$out/bin/hjwasm.exe"
          install -Dm0755 DJGPPr/hjwasm.exe "$out/bin/uasm.exe"
        ''
      else
        ''
          install -Dt "$out/bin" -m0755 GccUnixR/uasm
        ''
    }
    install -Dt "$out/share/doc/${finalAttrs.pname}" -m0644 {Readme,History}.txt Doc/*

    runHook postInstall
  '';

  enableParallelBuilding = true;

  makefile =
    if stdenv.hostPlatform.isDarwin then
      "Makefile-OSX-Clang-64.mak"
    else if stdenv.hostPlatform.isWindows then
      "Makefile-DOS-GCC.mak"
    else
      "Makefile-Linux-GCC-64.mak";

  passthru.tests.version = testers.testVersion {
    version = "v${finalAttrs.version}";
    command = "uasm -h";
    package = uasm;
  };

  meta = {
    description = "Free MASM-compatible assembler based on JWasm";
    homepage = "https://www.terraspace.co.uk/uasm.html";
    license = lib.licenses.watcom;

    maintainers = with lib.maintainers; [
      zane
      ccicnce113424
    ];

    platforms = lib.platforms.unix ++ lib.platforms.windows;
    mainProgram = "uasm";
    broken = stdenv.hostPlatform.isDarwin;
  };
})
