{
  lib,
  stdenv,
  fetchurl,
  libx11,
  libxext,
  libxfixes,
  pkg-config,
  testers,
  writeScript,
  xorgproto,
}:
stdenv.mkDerivation (finalAttrs: {
  pname = "libxi";
  version = "1.8.3";

  src = fetchurl {
    url = "mirror://xorg/individual/lib/libXi-${finalAttrs.version}.tar.xz";
    hash = "sha256-etYAVvAa9PeGz+k7OncHRHcRYm/I2iY3vscakECbq+U=";
  };

  outputs = [
    "out"
    "dev"
    "man"
    "doc"
  ];

  strictDeps = true;
  nativeBuildInputs = [ pkg-config ];

  buildInputs = [
    xorgproto
    libx11
    libxext
    libxfixes
  ];

  propagatedBuildInputs = [
    xorgproto
    # header file dependencies
    libx11
    libxext
    libxfixes
  ];

  configureFlags =
    lib.optional (stdenv.hostPlatform != stdenv.buildPlatform) "xorg_cv_malloc0_returns_null=no"
    ++ lib.optional stdenv.hostPlatform.isStatic "--disable-shared";

  passthru = {
    tests.pkg-config = testers.testMetaPkgConfig finalAttrs.finalPackage;

    updateScript = writeScript "update-${finalAttrs.pname}" ''
      #!/usr/bin/env nix-shell
      #!nix-shell -i bash -p common-updater-scripts
      version="$(list-directory-versions --pname libXi \
        --url https://xorg.freedesktop.org/releases/individual/lib/ \
        | sort -V | tail -n1)"
      update-source-version ${finalAttrs.pname} "$version"
    '';
  };

  meta = {
    description = "library for the X Input Extension";
    homepage = "https://gitlab.freedesktop.org/xorg/lib/libxi";

    license = with lib.licenses; [
      mitOpenGroup
      hpnd
      mit
    ];

    maintainers = [ ];
    platforms = lib.platforms.unix;
    pkgConfigModules = [ "xi" ];
  };
})
