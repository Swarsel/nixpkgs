{
  lib,
  stdenv,
  autoreconfHook,
  fetchFromGitea,
  glib,
  gobject-introspection,
  gtk-doc,
  libtool,
  libxml2,
  libxslt,
  openssl,
  pkg-config,
  python3,
  writeShellScript,
  xmlsec,
  zlib,
}:

stdenv.mkDerivation (finalAttrs: {
  pname = "lasso";
  version = "2.9.0";

  src = fetchFromGitea {
    owner = "entrouvert";
    repo = "lasso";
    rev = "v${finalAttrs.version}";
    hash = "sha256-fDMM9DJBzxz6DX4cNK3DEE28FBT8gCF9C9DQfUNNFaY=";
    domain = "git.entrouvert.org";
  };

  postPatch =
    let
      printVersion = writeShellScript "print-version" ''
        echo -n ${lib.escapeShellArg finalAttrs.version}
      '';
    in
    ''
      cp ${printVersion} tools/git-version-gen
    '';

  nativeBuildInputs = [
    autoreconfHook
    pkg-config
    python3
    gobject-introspection
  ];

  buildInputs = [
    glib
    gtk-doc
    libtool
    libxml2
    libxslt
    openssl
    python3.pkgs.six
    xmlsec
    zlib
  ];

  configurePhase = ''
    ./configure --with-pkg-config=$PKG_CONFIG_PATH \
                --disable-perl \
                --prefix=$out
  '';

  meta = {
    description = "Liberty Alliance Single Sign-On library";
    homepage = "https://lasso.entrouvert.org/";
    changelog = "https://git.entrouvert.org/entrouvert/lasso/raw/tag/v${finalAttrs.version}/NEWS";
    license = lib.licenses.gpl2Plus;
    maintainers = with lib.maintainers; [ womfoo ];
    platforms = lib.platforms.linux;
  };
})
