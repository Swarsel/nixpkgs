{
  lib,
  fetchFromGitHub,
  # optional-dependencies
  # asdf
  asdf,
  asdf-astropy,
  # dependencies
  astropy,
  # net
  beautifulsoup4,
  buildPythonPackage,
  # map
  contourpy,
  # dask
  dask,
  drms,
  # build-system
  extension-helpers,
  fsspec,
  # jpeg
  glymur,
  # timeseries
  h5netcdf,
  h5py,
  # tests
  hypothesis,
  ipywidgets,
  # jupyter
  itables,
  lxml,
  matplotlib,
  numpy,
  # opencv
  opencv-python,
  packaging,
  pandas,
  parfive,
  pyerfa,
  pytest-astropy,
  pytest-mock,
  pytestCheckHook,
  python-dateutil,
  reproject,
  requests,
  responses,
  # scikit-image
  scikit-image,
  # image
  scipy,
  setuptools,
  setuptools-scm,
  tqdm,
  writableTmpDirAsHomeHook,
  zeep,
}:

buildPythonPackage (finalAttrs: {
  pname = "sunpy";
  version = "8.0.0";

  src = fetchFromGitHub {
    owner = "sunpy";
    repo = "sunpy";
    tag = "v${finalAttrs.version}";
    hash = "sha256-VR0FvIkskjL1rvc0xOp+DSS+ocTJAAk4NYkO8+kpqmA=";
  };

  nativeCheckInputs = [
    hypothesis
    pytest-astropy
    pytest-mock
    pytestCheckHook
    responses
    writableTmpDirAsHomeHook
  ]
  ++ finalAttrs.passthru.optional-dependencies.all;

  __structuredAttrs = true;

  build-system = [
    extension-helpers
    numpy
    setuptools
    setuptools-scm # Technically needs setuptools-scm[toml], but that's our default.
  ];

  dependencies = [
    astropy
    fsspec
    numpy
    packaging
    parfive
    pyerfa
    requests
  ]
  ++ parfive.optional-dependencies.ftp;

  disabledTestPaths = [
    # Tests are very slow
    "sunpy/net/tests/test_fido.py"
    "sunpy/net/tests/test_scraper.py"
    # asdf.extensions plugin issue
    "sunpy/io/special/asdf/resources/manifests/*.yaml"
    "sunpy/io/special/asdf/resources/schemas/"
    # Requires mpl-animators
    "sunpy/coordinates/tests/test_wcs_utils.py"
    "sunpy/image/tests/test_resample.py"
    "sunpy/image/tests/test_transform.py"
    "sunpy/io/special/asdf/tests/test_genericmap.py"
    "sunpy/map"
    "sunpy/net/jsoc/tests/test_jsoc.py"
    "sunpy/physics/differential_rotation.py"
    "sunpy/physics/tests/test_differential_rotation.py"
    "sunpy/net/soar/tests/test_sunpy_soar.py"
    "sunpy/visualization"

    # Requires cdflib
    "sunpy/io/tests/test_cdf.py"
    "sunpy/timeseries"
    # Requires jplephem
    "sunpy/io/special/asdf/tests/test_coordinate_frames.py"
    # Requires spiceypy
    "sunpy/coordinates/tests/test_spice.py"
  ];

  disabledTests = [
    "rst" # Docs
    "test_print_params" # Needs to be online
    "test_find_dependencies" # Needs cdflib
    # Needs mpl-animators
    "sunpy.coordinates.utils.GreatArc"
    "test_cutout_not_on_disk_when_tracking"
    "test_expand_list_generator_map"
    "test_great_arc_different_observer"
    "test_great_arc_points_differentiates"
    "test_great_arc_wrongly_formatted_points"
    "test_main_exclude_remote_data"
    "test_main_include_remote_data"
    "test_main_nonexisting_module"
    "test_main_only_remote_data"
    "test_main_stdlib_module"
    "test_main_submodule_map"
    "test_tai_seconds"
    "test_utime"

    # AssertionError: assert 2 == 1
    # where 2 = len(WarningsChecker(record=True))
    "test_sunpy_warnings_logging"
  ];

  optional-dependencies = lib.fix (self: {
    all = lib.concatLists [
      self.core
      self.asdf
      self.jpeg2000
      self.opencv
      # optional-dependencies.spice
      self.scikit-image
    ];

    asdf = [
      asdf
      asdf-astropy
    ];

    # We can't use `with` here because "map" would still be the builtin, and
    # we can't below because scikit-image would still be this package's argument.
    core = lib.concatLists [
      self.image
      self.map
      self.net
      self.timeseries
      self.visualization
    ];

    dask = [ dask ] ++ dask.optional-dependencies.array;
    image = [ scipy ];

    jpeg2000 = [
      glymur
      lxml
    ];

    jupyter = [
      itables
      ipywidgets
    ];

    map = [
      contourpy
      matplotlib
      # mpl-animators
      reproject
      scipy
    ];

    net = [
      beautifulsoup4
      drms
      python-dateutil
      tqdm
      zeep
    ];

    opencv = [ opencv-python ];
    scikit-image = [ scikit-image ];

    # spice = [ spiceypy ];
    timeseries = [
      # cdflib
      h5netcdf
      h5py
      matplotlib
      pandas
    ];

    visualization = [
      matplotlib
      # mpl-animators
    ];
  });

  pyproject = true;
  pythonImportsCheck = [ "sunpy" ];

  meta = {
    description = "Python for Solar Physics";
    homepage = "https://sunpy.org";
    changelog = "https://docs.sunpy.org/en/stable/whatsnew/changelog.html";
    license = lib.licenses.bsd2;
    maintainers = [ ];
    downloadPage = "https://github.com/sunpy/sunpy";
  };
})
