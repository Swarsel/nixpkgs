{
  lib,
  stdenv,
  fetchFromGitHub,
  asgiref,
  buildPythonPackage,
  nix-update-script,
  pytest-benchmark,
  pytestCheckHook,
  setuptools,
  twisted,
}:

buildPythonPackage (finalAttrs: {
  pname = "prometheus-client";
  version = "0.25.0";

  src = fetchFromGitHub {
    owner = "prometheus";
    repo = "client_python";
    tag = "v${finalAttrs.version}";
    hash = "sha256-vue/5ulOnKkYjiHYWgT6HZ5mhV2vqAstm44+zwm+po0=";
  };

  nativeCheckInputs = [
    pytest-benchmark
    pytestCheckHook
  ]
  ++ lib.concatAttrValues finalAttrs.passthru.optional-dependencies;

  __darwinAllowLocalNetworking = true;
  build-system = [ setuptools ];
  dependencies = [ asgiref ];

  disabledTests = lib.optionals stdenv.hostPlatform.isDarwin [
    # fails in darwin sandbox: Operation not permitted
    "test_instance_ip_grouping_key"
  ];

  optional-dependencies.twisted = [ twisted ];
  pyproject = true;
  pytestFlags = [ "--benchmark-disable" ];
  pythonImportsCheck = [ "prometheus_client" ];
  passthru.updateScript = nix-update-script { };

  meta = {
    description = "Prometheus instrumentation library for Python applications";
    homepage = "https://github.com/prometheus/client_python";
    changelog = "https://github.com/prometheus/client_python/releases/tag/${finalAttrs.src.tag}";
    license = lib.licenses.asl20;
    maintainers = [ ];
  };
})
