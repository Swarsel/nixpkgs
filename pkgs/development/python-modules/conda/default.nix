{
  lib,
  stdenv,
  fetchFromGitHub,
  # runtime dependencies
  archspec,
  buildPythonPackage,
  conda-libmamba-solver,
  conda-package-handling,
  distro,
  frozendict,
  hatch-vcs,
  # build dependencies
  hatchling,
  jsonpatch,
  packaging,
  platformdirs,
  pluggy,
  pycosat,
  requests,
  ruamel-yaml,
  tqdm,
  truststore,
  # runtime options
  defaultEnvPath ? "~/.conda/envs", # default path to store conda environments
  defaultPkgPath ? "~/.conda/pkgs", # default path to store download conda packages
}:
buildPythonPackage rec {
  pname = "conda";
  version = "26.5.2";

  src = fetchFromGitHub {
    inherit pname version;
    owner = "conda";
    repo = "conda";
    tag = version;
    hash = "sha256-hiH25EcybtyEuks496VgiP4TPwNKI3x1URfwuefJRls=";
  };

  patches = [ ./0001-conda_exe.patch ];
  __structuredAttrs = true;

  build-system = [
    hatchling
    hatch-vcs
  ];

  dependencies = [
    archspec
    conda-libmamba-solver
    conda-package-handling
    distro
    frozendict
    jsonpatch
    packaging
    platformdirs
    pluggy
    pycosat
    requests
    ruamel-yaml
    tqdm
    truststore
  ];

  makeWrapperArgs = [
    "--set"
    "CONDA_EXE"
    "${placeholder "out"}/bin/conda"

    "--set-default"
    "CONDA_ENVS_PATH"
    defaultEnvPath

    "--set-default"
    "CONDA_PKGS_DIRS"
    defaultPkgPath
  ];

  pyproject = true;
  pythonImportsCheck = [ "conda" ];
  pythonRelaxDeps = [ "ruamel-yaml" ];
  # menuinst is currently not packaged
  pythonRemoveDeps = lib.optionals (!stdenv.hostPlatform.isWindows) [ "menuinst" ];

  meta = {
    description = "OS-agnostic, system-level binary package manager";
    homepage = "https://github.com/conda/conda";
    license = lib.licenses.bsd3;
    maintainers = [ lib.maintainers.ericthemagician ];
  };
}
